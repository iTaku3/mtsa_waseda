const MAX = 2
range R = 0..MAX


// -------- BEFORE ---------
set ControllableActions = {takeoff, land, blink, read, switchE, charge}
set OLDControllableActions = {takeoffOLD, landOLD, blinkOLD, readOLD}
set A = {ControllableActions, read.val[R], lowBattery, OLDControllableActions}


ARDRONE = LANDED,
LANDED = (takeoff -> AIR | lowBattery -> LANDED | charge -> LANDED | blink -> LANDED| read -> read.val[R] -> LANDED),
AIR = (land -> LANDED | lowBattery -> AIR | blink -> AIR | read -> read.val[R] -> AIR).

BATTERY_COUNTER(N=4) = BAT[N],
BAT[i:0..N]= (  when (i==2) lowBattery -> BAT[i-1] |
                                when (i!=2) {read, takeoff, land} -> BAT[i-1] |
                                charge -> BATTERY_COUNTER
                         ).

fluent LowBattery = <lowBattery, charge>
fluent Read = <{read, readOLD}, A\{read, readOLD}>
fluent Takeoff = <{takeoff, takeoffOLD}, A\{takeoff, takeoffOLD}>
fluent Land = <{land, landOLD}, A\{land, landOLD}>
fluent Blink = <{blink, blinkOLD}, A\{blink, blinkOLD}>
assert TRUE = (switchE || !switchE)

fluent IN_AIR = <{takeoff,takeoffOLD},{land,landOLD}> initially 0
assert ONLY_BLINK_IN_AIR = (Blink -> IN_AIR)
assert ONLY_READ_IN_AIR = (Read -> IN_AIR)
assert DONT_READ_TWICE_IN_AIR = (Read -> X(!Read W (!IN_AIR)))
assert DONT_READ_IF_LOW_BATTERY = (Read -> !LowBattery)

ltl_property Invariant = [](ONLY_READ_IN_AIR && ONLY_BLINK_IN_AIR && DONT_READ_TWICE_IN_AIR)

assert BLINK_ONLY_WITH_ONES = (read.val[1] -> PROCESS_VALUE && ((read.val[0] || read.val[2]) -> (!Blink W (Read || switchE))))
assert BLINK_ONLY_WITH_TWOS = ((read.val[2] -> PROCESS_VALUE) && ((read.val[0] || read.val[1]) -> (!Blink W Read)))
assert PROCESS_VALUE = ((!Read && !Land) W (Blink || switchE))


fluent SwitchE = <switchE, switchC> initially 0 // HACK! switchC is livenessd to not happen
fluent SwitchC = <switchC, landOLD> initially 0 // HACK! landOLD is livenessd to not happen
assert A_SwitchC = SwitchC

ltl_property S_OLD = (BLINK_ONLY_WITH_ONES W switchE)
ltl_property S_NEW = [](switchE -> [](BLINK_ONLY_WITH_TWOS && DONT_READ_IF_LOW_BATTERY))


ltl_property Transition1 = (TRUE)
ltl_property Transition2 = ( []((read.val[1] && (!Blink W switchE)) -> [](switchE -> ((!Read && !Land) W Blink))))
                                   // When switchE on unprocessed value, use OLD spec

ltl_property Transition3 = (
                                   []((read.val[1] && (!Blink W switchE)) -> [](switchE -> (!Blink W Read)))
                                  )     // When switchE on unprocessed value, use NEW spec









// E en fase 2 siga en el estado que estaba E en fase 1.
// Esto se puede hacer cambiando C para que se apague al recibir switchC.
// Entonces la composicion de C y E va a comportarse como E (fase 2) despues de switchC.

// El mismo problema existe en general para fase 3 aunque justo aca no se necesita.


        C_Original = Q0,
        Q0      = (takeoffOLD -> Q1 | switchC -> OFF),
        Q1      = (readOLD -> Q4 | switchC -> OFF),
        Q2      = (landOLD -> Q0 | switchC -> OFF),
        Q3      = (blinkOLD -> Q2 | switchC -> OFF),
        Q4      = (read.val.{[0], [2]} -> Q2
                  |read.val[1] -> Q3 | switchC -> OFF),
        OFF = ({takeoffOLD, landOLD, blinkOLD, readOLD, read.val[R]} -> OFF).

        E_OLDandNEW = Q0,
        Q0      = (blinkOLD -> Q0
                  |readOLD -> Q1
                  |takeoffOLD -> Q2
                  |switchC -> Q0_N),
        Q1      = (read.val[0..2] -> Q0
                  |switchC -> Q1_N),
        Q2      = (landOLD -> Q0
                  |blinkOLD -> Q2
                  |readOLD -> Q3
                  |switchC -> Q2_N),
        Q3      = (read.val[0..2] -> Q2
                  |switchC -> Q3_N),
        Q0_N    = (blink -> Q0_N
                  |read -> Q1_N
                  |takeoff -> Q2_N
                  |switchE -> Q0_NP),
        Q1_N    = (read.val[0..2] -> Q0_N
                  |switchE -> Q1_NP),
        Q2_N    = (land -> Q0_N
                  |blink -> Q2_N
                  |read -> Q3_N
                  |switchE -> Q2_NP),
        Q3_N    = (read.val[0..2] -> Q2_N
                  |switchE -> Q3_NP),


        Q0_NP   = (blink -> Q0_NP
                  |read -> Q1_NP
                  |takeoff -> Q2_NP
                  |charge->Q0_NP),
        Q1_NP   = (read.val[0..2] -> Q0_NP),
        Q2_NP   = (land -> Q0_NP
                  |blink -> Q2_NP
                  |read -> Q3_NP
                  |lowBattery -> Q2_NP),
        Q3_NP   = (read.val[0..2] -> Q2_NP).


        AUX_BATTERY_COUNTER(N=4) = Q0,
        Q0 = (switchE -> Q0  |
             {land, read, takeoff} -> BAT[N]),
        BAT[i:0..N]= (  when (i==2) lowBattery -> BAT[i-1] |
                                when (i!=2) {read, takeoff, land} -> BAT[i-1] |
                                charge -> BAT[N]
                         ).

    ||FASE1a3 = (C_Original || E_OLDandNEW || AUX_BATTERY_COUNTER(10)).

    ||TEST = (C||FASE1a3).

controllerSpec ObjectiveBefore = {
        safety = {S_OLD,S_NEW,Invariant,Transition3}
        assumption = {A_SwitchC}
        liveness = {Read,SwitchE}
        controllable = {ControllableActions}
}

controller ||C=(FASE1a3)~{ObjectiveBefore}.
