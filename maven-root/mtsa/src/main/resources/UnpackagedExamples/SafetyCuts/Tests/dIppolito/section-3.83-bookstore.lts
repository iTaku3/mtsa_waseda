set AlphaLib = {lib.{login, logout, search, order}}
set AlphaPay = {pay.{login, logout, pay}}
set AlphaCWS = {cws.{login, logout, getBook, gotBook, loginOk, logoutOk}}

set Controllable = {AlphaLib, AlphaPay, cws.{gotBook, loginOk, logoutOk}}
set A = {AlphaLib, AlphaPay, AlphaCWS}

//environment 
L = (login->LOGGED_IN),
LOGGED_IN = (logout->L | search->SEARCHING),
SEARCHING = (order->LOGGED_IN).
||LIB = (lib:L).

P = (login->LOGGED_IN),
LOGGED_IN = (logout->P | pay->LOGGED_IN).
||PAY = (pay:P).

C = (login->loginOk->LoggedIn),
LoggedIn = (logout->logoutOk->C | getBook->gotBook->LoggedIn).
||CWS = (cws:C).

||ENV = (PAY || LIB || CWS).

//fluents 
fluent F_BothLogin = <{{lib, pay}.{login}}, A\{{lib, pay}.{login}}>
fluent F_BothLogout = <{{lib, pay}.{logout}}, A\{{lib, pay}.{logout}}>

fluent F_CWSLoggedOut = <cws.logoutOk, cws.loginOk>
fluent F_CWSTryingLogout = <cws.logout, cws.logoutOk>
fluent F_CWSTryLogout = <cws.logout, A\{cws.logout}>
fluent F_CWSLogoutOk = <cws.logoutOk, A\{cws.logoutOk}>

fluent F_CWSLoggedIn = <cws.loginOk, cws.logoutOk>
fluent F_CWSTryingLogin = <cws.login, cws.loginOk>
fluent F_CWSTryLogin = <cws.login, A\{cws.login}>
fluent F_CWSLoginOk = <cws.loginOk, A\{cws.loginOk}>

fluent F_LibLoggedIn = <lib.login, lib.logout>
fluent F_PayLoggedIn = <pay.login, pay.logout>

fluent F_LibLoggedOut = <lib.logout, lib.login>
fluent F_PayLoggedOut = <pay.logout, pay.login>

fluent F_CWSGetBook = <cws.getBook, A\{cws.getBook}>
fluent F_GotBook = <cws.gotBook, A\{cws.gotBook}>

fluent F_GetBook = <cws.getBook, A\{cws.getBook}>
fluent F_ProsessingGetBook = <cws.getBook, cws.gotBook>
fluent F_Pay = <pay.pay, A\{pay.pay}>
fluent F_Ordered = <lib.order, {lib.{search, logout}}>

fluent F_Search = <lib.search, A\{lib.search}>
fluent F_OrderPending = <lib.order, cws.gotBook>

fluent F_Paid = <pay.pay, cws.getBook>//pay.logout>
fluent F_PayPending = <lib.order, pay.pay>

PAYMENT = NOT_PENDING,
NOT_PENDING = (lib.order->PENDING | {{A}\{lib.order, pay.pay}}->PAYMENT | pay.pay->ERROR ),
PENDING = ({{A}\{pay.pay}}->PENDING | pay.pay->NOT_PENDING).
||A = PAYMENT.

//safety
ltl_property CWSLogInOk = [](F_CWSLoginOk->(F_LibLoggedIn && F_PayLoggedIn))
ltl_property CWSLogOutOk = [](F_CWSLogoutOk->(F_LibLoggedOut && F_PayLoggedOut))
ltl_property Logins = [](F_BothLogin -> F_CWSTryingLogin)
ltl_property Logouts = [](F_BothLogout -> F_CWSTryingLogout)

ltl_property PayIfOrdered = [](F_Pay -> (F_PayPending))
ltl_property SearchIfLoggedIn = [](F_Search -> (!F_OrderPending && F_LibLoggedIn && F_PayLoggedIn))
ltl_property GetBookProcess = [](F_Search-> F_ProsessingGetBook)
ltl_property GetOnlyIfLoggedIn = [](F_CWSGetBook -> F_CWSLoggedIn)
ltl_property GotBookOnlyIfPaid = [](F_GotBook -> F_Paid)

assert GotBook = F_GotBook
assert GetBook = F_GetBook

//controllers
controller ||C = (ENV)~{G1}.
checkCompatibility ||Compatible = (ENV)~{G1}.

//controllerSpecs
controllerSpec G1 = {
       safety = {Logins, Logouts, SearchIfLoggedIn, 
				GetBookProcess, GotBookOnlyIfPaid, GetOnlyIfLoggedIn,
				PAYMENT}
//       failure = {Failures}
       assumption = {GetBook}
       liveness = {GotBook}
       controllable = {Controllable}
}

EXP  = Q0,
	Q0	= (cws.login -> Q23),
	Q1	= (lib.logout -> Q5
		  |cws.logoutOk -> Q20
		  |pay.logout -> Q21),
	Q2	= (cws.getBook -> Q10
		  |cws.logout -> Q24),
	Q3	= (cws.logout -> Q1
		  |cws.getBook -> Q10),
	Q4	= (pay.logout -> Q13
		  |cws.logoutOk -> Q29),
	Q5	= (cws.logoutOk -> Q14
		  |pay.logout -> Q25),
	Q6	= (pay.login -> Q16
		  |lib.login -> Q19),
	Q7	= (lib.order -> Q9),
	Q8	= (cws.loginOk -> Q3),
	Q9	= (pay.pay -> Q27),
	Q10	= (lib.search -> Q7),
	Q11	= (lib.logout -> Q13
		  |cws.logoutOk -> Q30),
	Q12	= (cws.getBook -> Q10
		  |cws.logout -> Q24),
	Q13	= (cws.logoutOk -> Q28),
	Q14	= (cws.login -> Q18),
	Q15	= (cws.loginOk -> Q2),
	Q16	= (lib.login -> Q15),
	Q17	= (cws.login -> Q22),
	Q18	= (lib.login -> Q8),
	Q19	= (pay.login -> Q15),
	Q20	= (cws.login -> Q8),
	Q21	= (cws.logoutOk -> Q17
		  |lib.logout -> Q25),
	Q22	= (pay.login -> Q8),
	Q23	= (pay.login -> Q18
		  |lib.login -> Q22),
	Q24	= (lib.logout -> Q4
		  |pay.logout -> Q11
		  |cws.logoutOk -> Q26),
	Q25	= (cws.logoutOk -> Q0),
	Q26	= (cws.login -> Q15),
	Q27	= (cws.gotBook -> Q12),
	Q28	= (cws.login -> Q6),
	Q29	= (cws.login -> Q16),
	Q30	= (cws.login -> Q19).
