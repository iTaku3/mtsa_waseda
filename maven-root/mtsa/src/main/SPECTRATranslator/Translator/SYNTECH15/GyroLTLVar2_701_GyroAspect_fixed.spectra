// Initial file contents for AspectLTL aspect file (*.altl)
module GyroAspect
		env {CLEAR, BLOCKED} frontDistSense ;
		env {CLEAR, BLOCKED} backDistSense ;
		env boolean isReady ;
		env {STP, FWD, BWD, TURN_LEFT, TURN_RIGHT, SWITCH_MODE, SPEED_DOWN, SPEED_UP, NOP } bluetoothCmd ;
		sys {STP, FWD, BWD, TURN_LEFT, TURN_RIGHT} balancer ;
		sys { SLOW, REGULAR, FAST } balancerSpeed ;
		sys { AUTO, MANUAL } spec_mode ;
		sys { STP, FWD, BWD, TURN_LEFT, TURN_RIGHT } spec_manualCmd ;


// Initial state.
guarantee
	balancer=STP;

// Stop while the robot is not ready to move.
guarantee
	G (!isReady -> balancer=STP);

// Move forward only when there are no obstacles ahead.
guarantee 
	G ((balancer=FWD) -> (frontDistSense=CLEAR));

// Move backward only when there are no obstacles behind.
guarantee 
	G ((balancer=BWD) -> (backDistSense=CLEAR));

// When in auto mode, eventually move forward.
guarantee 
  GF (spec_mode=AUTO->balancer=FWD);

// Pattern #20 for: turning happens when blocked until clear
////////////////////////////////////////////////////////////
define 
  	turningOp := (balancer=TURN_RIGHT | balancer=TURN_LEFT); 
  	
  sys { S0, S1, S2} spec_blockedState ;

guarantee -- initial assignments: initial state
  spec_blockedState=S0;

guarantee -- safety this and next blockedState
  G ((spec_mode=AUTO & isReady) -> ((spec_blockedState=S0 & ((!(frontDistSense=BLOCKED) & !(frontDistSense=CLEAR)) | (frontDistSense=CLEAR)) & next(spec_blockedState=S0)) |
  (spec_blockedState=S0 & (frontDistSense=BLOCKED & !turningOp & !(frontDistSense=CLEAR)) & next(spec_blockedState=S1)) |
  (spec_blockedState=S0 & (frontDistSense=BLOCKED & turningOp & !(frontDistSense=CLEAR)) & next(spec_blockedState=S2)) |
  (spec_blockedState=S1 & next(spec_blockedState=S1)) |
  (spec_blockedState=S2 & (frontDistSense=CLEAR) & next(spec_blockedState=S0)) |
  (spec_blockedState=S2 & (!turningOp & !(frontDistSense=CLEAR)) & next(spec_blockedState=S1)) |
  (spec_blockedState=S2 & (turningOp & !(frontDistSense=CLEAR)) & next(spec_blockedState=S2))));

guarantee -- equivalence of satisfaction
  GF (spec_mode=AUTO->(spec_blockedState=S0|spec_blockedState=S2));
////////////////////////////////////////////////////////////

// Switching between auto and manual modes logic:
////////////////////////////////////////////////////////////

guarantee -- initial assignments: initial state
  spec_mode=AUTO;

guarantee -- safety this and next blockedState
  G ((spec_mode=AUTO & bluetoothCmd=SWITCH_MODE & next(spec_mode=MANUAL) & (spec_manualCmd=STP)) | 
  	(spec_mode=MANUAL & bluetoothCmd=SWITCH_MODE & next(spec_mode=AUTO)) | 
  	(bluetoothCmd!=SWITCH_MODE & next(spec_mode)=spec_mode)
  	);

////////////////////////////////////////////////////////////

// Changing speed logic:
////////////////////////////////////////////////////////////

guarantee -- initial assignments: initial state
  balancerSpeed=REGULAR;

guarantee -- safety this and next blockedState
  G ((balancerSpeed=SLOW & bluetoothCmd=SPEED_DOWN & next(balancerSpeed=SLOW)) | 
  	(balancerSpeed=SLOW & bluetoothCmd=SPEED_UP & next(balancerSpeed=REGULAR)) | 
  	(balancerSpeed=REGULAR & bluetoothCmd=SPEED_DOWN & next(balancerSpeed=SLOW)) | 
  	(balancerSpeed=REGULAR & bluetoothCmd=SPEED_UP & next(balancerSpeed=FAST)) | 
  	(balancerSpeed=FAST & bluetoothCmd=SPEED_DOWN & next(balancerSpeed=REGULAR)) | 
  	(balancerSpeed=FAST & bluetoothCmd=SPEED_UP & next(balancerSpeed=FAST)) | 
  	((bluetoothCmd!=SPEED_DOWN & bluetoothCmd!=SPEED_UP) & next(balancerSpeed)=balancerSpeed)
  	);

////////////////////////////////////////////////////////////


// Manual mode specifications:
////////////////////////////////////////////////////////////

// Connecting bluetooth command to manual command local variable
guarantee
	G ((spec_mode=MANUAL) -> 
		(((bluetoothCmd=STP) -> (spec_manualCmd=STP)) &
		((bluetoothCmd=FWD) -> (spec_manualCmd=FWD)) &
		((bluetoothCmd=BWD) -> (spec_manualCmd=BWD)) &
		((bluetoothCmd=TURN_LEFT) -> (spec_manualCmd=TURN_LEFT)) &
		((bluetoothCmd=TURN_RIGHT) -> (spec_manualCmd=TURN_RIGHT))
	));

// Any non driving command from the bluetooth controller saves the last manual mode command
define
	bluetoothDrivingCmd := ((bluetoothCmd=STP) | (bluetoothCmd=FWD) | (bluetoothCmd=BWD) | (bluetoothCmd=TURN_LEFT) | (bluetoothCmd=TURN_RIGHT));
	
guarantee
	G ((spec_mode=MANUAL) -> 
		((!bluetoothDrivingCmd) -> (
		(PREV(spec_manualCmd=STP) -> spec_manualCmd=STP) &
		(PREV(spec_manualCmd=FWD) -> spec_manualCmd=FWD) &
		(PREV(spec_manualCmd=BWD) -> spec_manualCmd=BWD) &
		(PREV(spec_manualCmd=TURN_LEFT) -> spec_manualCmd=TURN_LEFT) &
		(PREV(spec_manualCmd=TURN_RIGHT) -> spec_manualCmd=TURN_RIGHT)
	)));	
	
guarantee
	G ((spec_mode=MANUAL) -> 
		((spec_manualCmd=FWD) -> ((frontDistSense=CLEAR & isReady)-> balancer=FWD))
	);
	
guarantee
	G ((spec_mode=MANUAL) -> 
		((spec_manualCmd=BWD) -> ((backDistSense=CLEAR & isReady) -> balancer=BWD))
	);
	
guarantee
	G ((spec_mode=MANUAL) -> 
		((spec_manualCmd=TURN_RIGHT) -> (isReady -> balancer=TURN_RIGHT))
	);
	
guarantee
	G ((spec_mode=MANUAL) -> 
		((spec_manualCmd=TURN_LEFT) -> (isReady -> balancer=TURN_LEFT))
	);
	
guarantee
	G ((spec_mode=MANUAL) -> 
		((spec_manualCmd=STP | (spec_manualCmd=FWD & frontDistSense=BLOCKED) | (spec_manualCmd=BWD & backDistSense=BLOCKED)) -> (isReady -> balancer=STP))
	);
	
////////////////////////////////////////////////////////////

// The robot needs to be stabilized at first and it is not ready to move.
assumption
	!isReady;

assumption
	bluetoothCmd=NOP;
		
// Robot starts when not blocked forward and backward
assumption
	frontDistSense=CLEAR & backDistSense=CLEAR;

// Eventually the robot will be ready to move.
assumption
  GF (isReady);

// When the robot is ready, it will stay that way.
assumption
	G (isReady -> next(isReady));

define 
  	moveAwayFromFrontObstacle := !(balancer=FWD | balancer=STP); 

// Front surface becomes clear responds to continuously moving away from obstacle.
assumption
  respondsTo(moveAwayFromFrontObstacle,(!moveAwayFromFrontObstacle|frontDistSense=CLEAR));

define 
  	moveAwayFromBackObstacle := !(balancer=BWD | balancer=STP); 

// Back surface becomes clear responds to continuously moving away from obstacle.
assumption
  respondsTo(moveAwayFromBackObstacle,(!moveAwayFromBackObstacle|backDistSense=CLEAR));

// Front surface becomes blocked responds to continuously moving forward.
assumption
  respondsTo(balancer=FWD,(!(balancer=FWD)|frontDistSense=BLOCKED));
	
// Back surface becomes blocked responds to continuously moving backward
assumption
  respondsTo(balancer=BWD,(!(balancer=BWD)|backDistSense=BLOCKED));

pattern respondsTo(trigger, response) {
  var boolean responded;
  responded;
  G (next(responded) iff (response or responded and !trigger));
  GF (responded);
}
