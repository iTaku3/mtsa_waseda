// Initial file contents for AspectLTL aspect file (*.altl)
module AspectLTLAspect
 
		env {CLEAR, BLOCKED} obstacle ;
		
		env {	TOO_CLOSE,
							TOO_FAR,
							CENTERED,
							PS_TOO_FAR,
							PS_TOO_CLOSE,
							PS_CENTERED
						 } sideSensor ;
						 
		env {	TOO_CLOSE,
								TOO_FAR,
								CENTERED,
								PS_TOO_FAR,
								PS_TOO_CLOSE,
								PS_CENTERED
							 } rearSideSensor ;
							
	
		sys {MAX_RIGHT, MAX_LEFT, CENTER, ALIGN_RIGHT, ALIGN_LEFT} steeringMot ;
		
		sys {FWD, BWD} sensorMot ;
							
		sys {FWD, BWD, STOP, PARKING_BWD, PARKING_FWD, PREPARE_PERPENDICULAR_BWD, PERPENDICULAR_BWD} engineMot ;
		
		sys {	SEEKING_FOR_PS, 
							MEASURE_PARKING,
							PREPARE_FOR_PARALLEL_PARKING,
							ALIGN_BEFORE_PARKING,
							PARALLEL,
							PERPENDICULAR,
							PARKED
						} carStatus ;
						
		sys Int(0..14) spec_counter ;
		
		sys {PARALLEL, PERPENDICULAR} spec_parkingType ;
			
	define
		counterLimit := 14;
		
	define 
		phaseNone := 0;
		
	define 
		firstPhase := 1;
		
	define 
		secondPhase := 2;
		
	define 
		thirdPhase := 3;
			
	define
		weGotSideSensorSeekingValues := (sideSensor = TOO_CLOSE | sideSensor = TOO_FAR | sideSensor = CENTERED);
		
	define
		weGotSideSensorMeasuringValues := (sideSensor = PS_TOO_CLOSE | sideSensor = PS_TOO_FAR | sideSensor = PS_CENTERED);	
	
	define
		Stop := (engineMot = STOP);

	define
		suitableParallelPS := 6;
		
	define
		suitablePerpendicularPS := 4;
		
	define
		preparingParallelSteps := 2;
	
	define
		capablePerpendicular := (spec_counter >= suitablePerpendicularPS);
	
	define
		keepCarStatus := (next(carStatus) = carStatus);
		
	define
		keepCounterValue := (next(spec_counter) = spec_counter);
		
	define
		keepParkingType := (next(spec_parkingType) = spec_parkingType);

	define
		movingFwd := (engineMot = FWD | engineMot = PARKING_FWD);
		
	define
		movingBwd := (engineMot = BWD | 
						engineMot = PARKING_BWD | 
						engineMot = PREPARE_PERPENDICULAR_BWD | 
						engineMot = PERPENDICULAR_BWD
		);
	
	define
		rearTooFar :=   (rearSideSensor = TOO_FAR | rearSideSensor = PS_TOO_FAR);
	
	define
		rearTooClose := (rearSideSensor = TOO_CLOSE | rearSideSensor = PS_TOO_CLOSE);
		
	define
		rearCentered := (rearSideSensor = CENTERED | rearSideSensor = PS_CENTERED);
		
	define
		frontCentered := (sideSensor = CENTERED | sideSensor = PS_CENTERED);
		
	define
		frontTooFar :=   (sideSensor = TOO_FAR | sideSensor = PS_TOO_FAR);
	
	define
		frontTooClose := (sideSensor = TOO_CLOSE | sideSensor = PS_TOO_CLOSE);
		

		
	/* Init state*/
	guarantee
		(carStatus = SEEKING_FOR_PS & sensorMot = FWD & spec_counter = 0);
		
	/* Do not run into obstacles*/
	guarantee
		G ((obstacle = BLOCKED) -> 
			(Stop)
		);
		
	/* In case of an obstacle, keep the car status and the counter value as they were
	 */
	guarantee
		G ((obstacle = BLOCKED
		) -> 
			(keepCarStatus & 
				keepCounterValue
			)
		);

	
	/* Look where you are driving */
	guarantee
		G(movingBwd -> sensorMot = BWD);
		
	guarantee
		G(movingFwd -> sensorMot = FWD);
		
	/* Final goal! */
	guarantee
  GF (carStatus=PARKED);
		
	/* Reached the final goal */
	guarantee
		G((carStatus = PARKED
		) ->
			((Stop) & (keepCarStatus) & steeringMot = CENTER )
		);
		
/**********************************************************************************/
/******************************** wall alignment **********************************/
/**********************************************************************************/
	
	/* set of options available while seeking for parking spot */
	guarantee
		G((carStatus = SEEKING_FOR_PS)-> 
			((steeringMot = ALIGN_RIGHT | steeringMot = CENTER | steeringMot = ALIGN_LEFT) & 
				(engineMot = FWD | engineMot = STOP) & (spec_counter = 0)
			)
		);
	
	/* Wall alignment instructions */
	guarantee
		G((carStatus = SEEKING_FOR_PS | carStatus = PREPARE_FOR_PARALLEL_PARKING | carStatus = MEASURE_PARKING)-> 
			(
				((sideSensor = TOO_CLOSE	| sideSensor = PS_TOO_CLOSE) 	& steeringMot = ALIGN_LEFT)		| 
				((sideSensor = TOO_FAR		| sideSensor = PS_TOO_FAR)		& steeringMot = ALIGN_RIGHT)	| 
				((sideSensor = CENTERED		| sideSensor = PS_CENTERED) 	& steeringMot = CENTER)
			)
		);
	
	/* If we still didn't find potential parking spot, and there's no obstacle - keep seeking */
	guarantee
		G((carStatus = SEEKING_FOR_PS & 
			(weGotSideSensorSeekingValues) & 
			obstacle = CLEAR
		) -> 
			(keepCarStatus)
		);
	
	/* A potential parking spot was found, and there's no obstacle - move to measuring mode */
	guarantee
		G((carStatus = SEEKING_FOR_PS & 
			(weGotSideSensorMeasuringValues) & 
			obstacle = CLEAR
		)
			 -> (
			 	(engineMot = STOP) &
			 	(next(carStatus = MEASURE_PARKING)) & 
			 	(next(spec_counter) = 1)
			)
		);
		
	
	/* V2 - We don't bypass obstacles */
	assumption
  respondsTo(obstacle=BLOCKED,(obstacle=CLEAR));

/**********************************************************************************/
/********************** measure parking spaces specs ******************************/
/**********************************************************************************/
	/* Set of options available while measuring for parking spot */
	guarantee
		G((carStatus = MEASURE_PARKING)-> 
			((steeringMot = ALIGN_RIGHT | steeringMot = CENTER | steeringMot = ALIGN_LEFT) & 
			(engineMot = FWD | engineMot = STOP) & (spec_counter <= suitableParallelPS))
		);
	
	/* Don't stop if you're measuring parking spot, and there's no obstacle */
	guarantee
		G((carStatus = MEASURE_PARKING & obstacle = CLEAR & weGotSideSensorMeasuringValues)	->
			(engineMot != STOP)
		);	

	/* If we are measuring a parking spot and we didn't measure enough space, keep measuring */
	guarantee
		G((carStatus = MEASURE_PARKING & 
			(weGotSideSensorMeasuringValues) & 
			(spec_counter < suitableParallelPS) &
			obstacle = CLEAR
		) -> 
			(keepCarStatus)
		);
	
	/* Do not count over the limit */
	guarantee
		G((spec_counter <= counterLimit));
		
	/* If the parking spot ended and there is no sufficient space (not parallel and not perpendicular), 
	 * keep seeking for another parking spot
	 */
	guarantee
		G((carStatus = MEASURE_PARKING & 
			(weGotSideSensorSeekingValues) & 
			obstacle = CLEAR & 
			spec_counter < suitableParallelPS & 
			!capablePerpendicular 
		) -> 
			(next(carStatus = SEEKING_FOR_PS)) 
		);
		
	/* If the parking spot ended and we can park perpendicular, move to park perpendicular */
	guarantee
		G((carStatus = MEASURE_PARKING & 
			(weGotSideSensorSeekingValues) & 
			obstacle = CLEAR & 
			spec_counter < suitableParallelPS & 
			capablePerpendicular 
		) -> 
			(next(carStatus = ALIGN_BEFORE_PARKING) &
				(next(spec_parkingType) = PERPENDICULAR) &
				next(spec_counter = 0)
			) 
		);
	
	/* While measuring the parking spot, increase counter if needed */
	guarantee
		G((carStatus = MEASURE_PARKING & 
			(weGotSideSensorMeasuringValues) & 
			obstacle = CLEAR & 
			engineMot = FWD & 
			spec_counter < suitableParallelPS
		) ->
			(next(spec_counter) = (spec_counter + 1))
		);
	
	/* If the parking spot ended and we can park parallel, move to preparing to parllel */
	guarantee
		G ((carStatus = MEASURE_PARKING & 
			spec_counter = suitableParallelPS  & 
			obstacle = CLEAR 
		) -> 
			(
				(next(carStatus) = PREPARE_FOR_PARALLEL_PARKING) &
				(next(spec_counter) = 0)
				));
				
	/* Goal for this stage */		
	guarantee
  respondsTo(carStatus=MEASURE_PARKING,(carStatus=PREPARE_FOR_PARALLEL_PARKING|carStatus=ALIGN_BEFORE_PARKING));
		
	/* How to achieve the goal above */
	assumption 
  respondsTo(engineMot=FWD,(carStatus=PREPARE_FOR_PARALLEL_PARKING|carStatus=ALIGN_BEFORE_PARKING));

/**********************************************************************************/
/*************************** Preparing for parallel parking************************/
/**********************************************************************************/
		
	/* Set of options available while preparing for parking */
	guarantee
		G((carStatus = PREPARE_FOR_PARALLEL_PARKING)-> 
			((steeringMot = ALIGN_RIGHT | steeringMot = CENTER | steeringMot = ALIGN_LEFT) & 
			(engineMot = FWD | engineMot = STOP))
		);
		
		
	/* Keep performing the needed steps forward in order to prepare for parallel parking */
	guarantee
		G((carStatus = PREPARE_FOR_PARALLEL_PARKING & obstacle = CLEAR
			& spec_counter < preparingParallelSteps
		) ->
			((next(spec_counter) = (spec_counter + 1)) & engineMot = FWD)
		);
	
	/* Keep the car status while performing the preparing, even when you are blocked~ */	
	guarantee
		G((carStatus = PREPARE_FOR_PARALLEL_PARKING & 
			(spec_counter < preparingParallelSteps)
		) -> 
			(keepCarStatus)
		);
	
	/* We completed the preparing steps, now move to actually park paralell */
	guarantee
		G ((carStatus = PREPARE_FOR_PARALLEL_PARKING & 
			spec_counter = preparingParallelSteps & 
			obstacle = CLEAR
		) -> 
			( 
				(Stop) &
				(next(carStatus) = ALIGN_BEFORE_PARKING) &
				(next(spec_parkingType) = PARALLEL) &
				(next(spec_counter) = 0)
				));

    /* Perform the necessary actions in order to reach the suitable number of steps before parking */
	guarantee
  respondsTo(carStatus=PREPARE_FOR_PARALLEL_PARKING,(spec_counter=preparingParallelSteps));
	
	/* How to achieve the goal above */
	assumption 
  respondsTo(((obstacle=CLEAR&carStatus=PREPARE_FOR_PARALLEL_PARKING)->(engineMot=FWD)),(carStatus=ALIGN_BEFORE_PARKING));
		
	
///**********************************************************************************/
///************************align before parking parallel ****************************/
///**********************************************************************************/

	/*corecting the angle to the left - those set of specs are dealing with the case where
	SPARC rear side sensor is too far from the wall/PS and the front side sensor is too close*/
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 0 & 
			obstacle = CLEAR &
			((rearTooFar & (frontCentered | frontTooClose)) | (rearCentered & frontTooClose)))-> 
			(sensorMot = BWD & Stop & keepCarStatus & next(spec_counter = 1))
			);
	
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & spec_counter = 1 & obstacle = CLEAR) -> 
			(engineMot = BWD & keepCarStatus & next(spec_counter = 2) & steeringMot = ALIGN_RIGHT)		
		);
	
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & spec_counter = 2 & obstacle = CLEAR ) -> 
			(sensorMot = FWD & Stop & keepCarStatus & next(spec_counter = 3))		
		);
		
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & spec_counter = 3 & obstacle = CLEAR) -> 
			(engineMot = FWD & keepCarStatus & next(spec_counter = 0) & steeringMot = CENTER)		
		);
	/*end of corecting the angle to the left */

	/*corecting the angle to the right - those set of specs are dealing with the case where
	SPARC front side sensor is too far from the wall/PS and the rear side sensor is too close*/
	
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 0 & obstacle = CLEAR &
			((frontTooFar & (rearCentered | rearTooClose)) | (frontCentered & rearTooClose)))-> 
			(engineMot = FWD & keepCarStatus & next(spec_counter = 4) & (steeringMot = ALIGN_RIGHT))
			);
	
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 4 &
			obstacle = CLEAR)->
			(sensorMot = BWD & Stop & keepCarStatus & next(spec_counter = 5))
		);
		
	guarantee
		G ((carStatus = ALIGN_BEFORE_PARKING & spec_counter = 5 & obstacle = CLEAR)->
			(engineMot = BWD & keepCarStatus & next(spec_counter = 0) & steeringMot = CENTER)
		);
	/*end of corecting the angle to the right */

	/*corecting the angle to the left2 - those set of specs are dealing with the case where
	SPARC rear side sensor is too far from the wall/PS and the front side sensor is also too far*/
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 0 & 
			obstacle = CLEAR &
			(rearTooFar & frontTooFar))-> 
			(engineMot = FWD & steeringMot = ALIGN_RIGHT & keepCarStatus & next(spec_counter = 6))
			);
			
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 6 &
			obstacle = CLEAR) ->
			(engineMot = FWD & steeringMot=ALIGN_LEFT & keepCarStatus & next(spec_counter = 7))
			);
			
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 7 &
			obstacle = CLEAR) ->
			(sensorMot = BWD & Stop & keepCarStatus & next(spec_counter = 8))
			);
	
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 8 &
			obstacle = CLEAR) ->
			(engineMot = BWD & keepCarStatus & next(spec_counter = 9) & steeringMot = CENTER)
			);
			
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 9 &
			obstacle = CLEAR) ->
			(engineMot = BWD & keepCarStatus & next(spec_counter = 0) & steeringMot = CENTER)
			);
	/*end of corecting the angle to the left2 */	

	/*corecting the angle to the right2 - those set of specs are dealing with the case where
	SPARC rear side sensor is too close from the wall/PS and the front side sensor is also too close*/
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 0 &
			obstacle = CLEAR &
			(rearTooClose & frontTooClose))-> 
			(engineMot = FWD & steeringMot = ALIGN_LEFT & keepCarStatus & next(spec_counter = 10))
			);
			
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 10 &
			obstacle = CLEAR) ->
			(engineMot = FWD & steeringMot=ALIGN_RIGHT & keepCarStatus & next(spec_counter = 11))
			);
			
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 11 &
			obstacle = CLEAR) ->
			(sensorMot = BWD & Stop & keepCarStatus & next(spec_counter = 12))
			);
	
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 12 &
			obstacle = CLEAR) ->
			(engineMot = BWD & keepCarStatus & next(spec_counter = 13) & steeringMot = CENTER)
			);
			
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 13 &
			obstacle = CLEAR) ->
			(engineMot = BWD & keepCarStatus & next(spec_counter = 0) & steeringMot = CENTER)
			);
	/*end of corecting the angle to the right2 */
		
		
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 0 & 
			spec_parkingType = PERPENDICULAR & 
			rearCentered & 
			frontCentered &
			obstacle = CLEAR
		) -> 
			((sensorMot = BWD) & Stop & next(spec_counter = firstPhase) & next(carStatus) = PERPENDICULAR)
		);
		
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING & 
			spec_counter = 0 & 
			spec_parkingType = PARALLEL & 
			rearCentered & 
			frontCentered &
			obstacle = CLEAR
		) -> 
			((sensorMot = BWD) & Stop & next(spec_counter = firstPhase) & next(carStatus) = PARALLEL)
		);
		
	guarantee
		G((carStatus = ALIGN_BEFORE_PARKING) -> keepParkingType);
	
	guarantee
  respondsTo((carStatus=ALIGN_BEFORE_PARKING),(carStatus=PERPENDICULAR|carStatus=PARALLEL));
		
	assumption
  respondsTo((carStatus=ALIGN_BEFORE_PARKING&(spec_counter=0)),(carStatus=PERPENDICULAR|carStatus=PARALLEL));
		
/**********************************************************************************/
/******************************** parking parallel ********************************/
/**********************************************************************************/

	guarantee
		G((obstacle = BLOCKED & 
			carStatus = PARALLEL & 
			(PREV(sensorMot = BWD) & !movingFwd)
		) -> (sensorMot = BWD));
	/* Set of options available while parking parallel */
	
	guarantee
		G((carStatus = PARALLEL) ->
			((steeringMot = MAX_RIGHT | steeringMot = CENTER | steeringMot = MAX_LEFT) & 
			(engineMot = PARKING_BWD | engineMot = STOP))
		);
	
	/* Performing first step of the parallel parking */
	guarantee
		G((carStatus = PARALLEL & 
			spec_counter = firstPhase & 
			obstacle = CLEAR
		) ->
			(engineMot = PARKING_BWD  & (keepCarStatus) & steeringMot = MAX_RIGHT & next(spec_counter = secondPhase))
		);
	
	/* Performing second step of the parallel parking */
	guarantee
		G((carStatus = PARALLEL & 
			spec_counter = secondPhase & 
			obstacle = CLEAR
		) ->
			(engineMot = PARKING_BWD & (next(carStatus = PARKED)) & steeringMot = MAX_LEFT)
		);
	
/**********************************************************************************/
/****************************** parking perpendicular *****************************/
/**********************************************************************************/

	/* Set of options available while parking perpendicular */
	guarantee
		G((carStatus = PERPENDICULAR) ->
			((steeringMot = MAX_RIGHT | steeringMot = CENTER | steeringMot = MAX_LEFT) & 
			(engineMot = PARKING_FWD | engineMot = STOP | engineMot = PREPARE_PERPENDICULAR_BWD | engineMot = PERPENDICULAR_BWD))
		);
	
	/* Performing first step of the perpendicular parking */
	guarantee
		G((carStatus = PERPENDICULAR & 
			spec_counter = firstPhase & 
			obstacle = CLEAR
		) ->
			(engineMot = PREPARE_PERPENDICULAR_BWD  & (keepCarStatus) & steeringMot = CENTER & next(spec_counter = secondPhase))
		);	
	
	/* Performing second step of the perpendicular parking */
	guarantee
		G((carStatus = PERPENDICULAR & 
			spec_counter = secondPhase & 
			obstacle = CLEAR
		) ->
			(engineMot = PARKING_FWD  & (keepCarStatus) & steeringMot = MAX_LEFT & next(spec_counter = thirdPhase))
		);
	
	/* Performing third step of the perpendicular parking */
	guarantee
		G((carStatus = PERPENDICULAR & 
			spec_counter = thirdPhase & 
			obstacle = CLEAR
		) ->
			(engineMot = PERPENDICULAR_BWD & next(carStatus = PARKED) & steeringMot = MAX_RIGHT)
		);
		
		
	
pattern respondsTo(trigger, response) {
  var boolean responded;
  responded;
  G (next(responded) iff (response or responded and !trigger));
  GF (responded);
}
